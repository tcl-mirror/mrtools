[manpage_begin micca-DSL n 1.0b6]
[comment {
# This software is copyrighted 2015 - 2017 by G. Andrew Mangogna.
# The following terms apply to all files associated with the software unless
# explicitly disclaimed in individual files.
# 
# The authors hereby grant permission to use, copy, modify, distribute,
# and license this software and its documentation for any purpose, provided
# that existing copyright notices are retained in all copies and that this
# notice is included verbatim in any distributions. No written agreement,
# license, or royalty fee is required for any of the authorized uses.
# Modifications to this software may be copyrighted by their authors and
# need not follow the licensing terms described here, provided that the
# new terms are clearly indicated on the first page of each file where
# they apply.
# 
# IN NO EVENT SHALL THE AUTHORS OR DISTRIBUTORS BE LIABLE TO ANY PARTY FOR
# DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES ARISING
# OUT OF THE USE OF THIS SOFTWARE, ITS DOCUMENTATION, OR ANY DERIVATIVES
# THEREOF, EVEN IF THE AUTHORS HAVE BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE AUTHORS AND DISTRIBUTORS SPECIFICALLY DISCLAIM ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE, AND NON-INFRINGEMENT.  THIS SOFTWARE
# IS PROVIDED ON AN "AS IS" BASIS, AND THE AUTHORS AND DISTRIBUTORS HAVE
# NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES, ENHANCEMENTS,
# OR MODIFICATIONS.
# 
# GOVERNMENT USE: If you are acquiring this software on behalf of the
# U.S. government, the Government shall have only "Restricted Rights"
# in the software and related documentation as defined in the Federal
# Acquisition Regulations (FARs) in Clause 52.227.19 (c) (2).  If you
# are acquiring the software on behalf of the Department of Defense,
# the software shall be classified as "Commercial Computer Software"
# and the Government shall have only "Restricted Rights" as defined in
# Clause 252.227-7013 (c) (1) of DFARs.  Notwithstanding the foregoing,
# the authors grant the U.S. Government and others acting in its behalf
# permission to use and distribute the software in accordance with the
# terms specified in this license.
}]
[moddesc "Micca Configuration DSL"]
[titledesc "Commands for Defining a Domain"]
[copyright "2015 - 2017 by G. Andrew Mangogna"]
[description]
This manpage describes the [cmd micca] DSL.
When translating an executable model using [cmd micca],
the model is transcoded into the DSL described in this manual.
The DSL is implemented as an internal DSL
in Tcl and the script follows Tcl syntax rules.

[section Commands]

At the highest level,
the commands in this section allow you to define the characteristics
of a domain and its initial instance population.

[list_begin definitions]
[call [cmd domain] [arg name] [arg script]]

The [cmd domain] command defines a domain named, [arg name], and evaluates
[arg script] in a context where commands in [arg script] may be used to define
the characteristics of a domain.
The [arg script] argument should contain invocations of the
[sectref "Domain Commands" "domain commands"]
described below.

[example {
domain wmctrl {
    # A script containing domain commands to define the domain.
    ...
}
}]

[call [cmd population] [arg domain] [arg script]]

The [cmd population] command is used to define the initial instance
population for the domain named, [arg domain].
The [arg script] argument should contain commands
that define the attribute values, relationship linkages and
storage allocations for [arg domain].
It is evaluated in a context where the
[sectref "Population Commands" "population commands"]
described below are available.

[example {
population wmctrl {
    # A script containing populations commands to define the initial
    # instance population and storage allocation for the domain.
    ...
}
}]

[list_end]

[section "Domain Commands"]

The commands in this section are used to define the characteristics
of a domain using the [cmd micca] DSL.
These are the commands that are available when the [arg script]
argument of the [cmd domain] is evaluated.

[list_begin definitions]

[call [cmd class] [arg classname] [opt [arg script]]]
The [cmd class] command defines a class named, [arg classname].
The [arg script] argument is optional and if provided is
a script that is evaluated in a context where
[sectref "Class Commands" "class commands"]
are available to define the characteristics of the class.
The [cmd class] command may invoked multiple times for the same
[arg classname] and the class definition is cumulative.

[example {
class Pump {
    # Commands that define the characteristics of the Pump class.
    ...
}
}]

[call [cmd association] [arg name]\
    [arg [opt "-associator class"]]\
    [arg [opt -multiple]]\
    [arg [opt "-static | -dynamic"]]\
    [arg [opt --]]\
    [arg source] [arg spec] [arg target] [arg [opt script]]]

The [cmd association] command defines an association relationship
called, [arg name],
between a [arg source] class and [arg target] class.
Traditionally,
association names follow a naming convention of a capital R followed
by a number ([emph e.g.] R27),
but any non-empty string [emph not] starting with a tilde character (~)
is allowed.
The forward direction of navigating the relationship is from [arg source]
to [arg target].
For simple associations ([emph i.e.] those not involving a associator
class),
the [arg source] class serves as the refering class
([emph i.e.] it contains the referential attributes formalizing
the association)
and the [arg target] class serves as the referenced class
([emph i.e.] it contains the identifier to which the referential
attributes of the [arg source] class refer).
The conditionality and multiplicity of the association is given by the
[arg spec] argument.
The [arg spec] argument must take one of the following forms:

[list_begin options]
[opt_def "1--1"]
    The association is [emph one] to [emph one].
[opt_def 0..1--1]
[opt_def ?--1]
    The association is [emph "at most one"] to [emph one].
[opt_def 0..1--0..1]
[opt_def ?--?]
    The association is [emph "at most one"] to [emph "at most one"].
[opt_def 1..*--1]
[opt_def +--1]
    The association is [emph "one or more"] to [emph one].
[opt_def 0..*--1]
[opt_def *--1]
    The association is [emph "zero or more"] to [emph one].
[opt_def 1..*--0..1]
[opt_def +--?]
    The association is [emph "one or more"] to [emph "at most one"].
[opt_def 0..*--0..1]
[opt_def *--?]
    The association is [emph "zero or more"] to [emph "at most one"].
[opt_def 1..*--1..*]
[opt_def +--+]
    The association is [emph "one or more"] to [emph "one or more"].
[opt_def 0..*--1..*]
[opt_def *--+]
    The association is [emph "zero or more"] to [emph "one or more"].
[opt_def 1..*--0..*]
[opt_def +--*]
    The association is [emph "one or more"] to [emph "zero or more"].
[opt_def 0..*--0..*]
[opt_def *--*]
    The association is [emph "zero or more"] to [emph "zero or more"].
[list_end]


A class based association is indicated by the [arg -associator] argument
with [arg class] being defined as the associator class.
Note that [cmd micca] will insist that any association whose
multiplicity and conditionality on the [arg target] class side
is [var 0..1] or where both sides have a multiplicity greater than one
be formalized using an associator class.
This is the same as disallowing any of a [emph NULL] value for
a referential attribute and insisting the instances of the
association track the instances of the associator class.

For class bases associations, the [arg -multiple] argument may be present
to indicate that multiple instances of the associator class may reference
the same instances of the participating classes.
This option is only allowed for class bases associations and is used
to define many-to-many-to-many relationships.

If the [arg -static] argument is given then the association is
defined to be static and the number of instances of the association 
itself cannot vary during the run time of the domain.
If the [arg -dynamic] argument is given or neither [arg -static]
or [arg -dynamic] is given,
then the instances of the association are assumed to vary during the
run time of the domain.
These options allow [cmd micca] to choose the appropriate data structures
for relationship storage given the run time behavior of the relationship.

The association command may have an optional [arg script] argument.
If [arg script] is present,
then the association is defined to have an assigner and
[arg script] is evaluated to define the characteristics of the assigner.
The [arg script] is evaluated in a context where
[sectref "Assigner Commands" "assigner commands"]
are available to define the characteristics of the association assigner.

[example {
association R13 -static WashingMachine 1--1 Tub

association R22 -associator Ownership Dog 1..*--0..* Owner

association R2 -associator Service Clerk 0..1--0..1 Client {
    statemodel {
        # State model for assigning clerks to clients.
    }
}
}]


[call [cmd generalization] [arg name] [arg [opt "-union | -reference"]]\
        [arg super] [arg sub1] [arg sub2] [arg [opt ...]]]

The [cmd generalization] command defines a generalization relationship
called, [arg name].
Traditionally,
generalization names follow a naming convention of a capital R followed
by a number ([emph e.g.] R27),
but any non-empty string [emph not] starting with a tilde character (~)
is allowed.
The [arg super] is the name of a class that will serve as the superclass
in the generalization.
The [arg subN] arguments give the names of the subclasses of the
generalization.
At least two subclasses must be given.
If the [arg -union] argument is given,
then subclasses will be stored in a union within the structure of the
superclass.
If the [arg -reference] argument is given or if neither [arg -union]
nor [arg -reference] is present,
the subclasses are stored separate from the superclass and instance
references are used to implement the generalization relationship.

[example {
generalization R12 Lamp TableLamp FloorLamp

generalization R32 -union Washer FrontLoader TopLoader
}]

[call [cmd domainop] [arg rettype] [arg name] [arg parameters] [arg body]\
        [arg [opt comment]]]

The [cmd domainop] command defines a domain operation called, [arg name].
A domain operation is translated to a "C" function of external scope.
The "C" return type of the function is given by the [arg rettype] argument.
Formal parameters to the operation are given as a list
of parameter name / parameter type pairs in the [arg parameters] argument.
The code for the domain operation is supplied in the [arg body]
argument.
If present, the optional [arg comment] argument text is inserted as
a comment in the generated header file for the domain just before the
external declaration of the domain operation function.
This provides a convenient way to provide addition information to users
of the domain operation in the generated header file.

[example {
domainop void startWasher {washerID MRT_InstId when MRT_DelayTime} {
    // C Code to start a washing machine.
} {
Use this operation to start a washing machine.
}
}]

[call [cmd eentity] [arg entityname] [arg script]]

The [cmd eentity] command defines an external entity which serves
as a proxy for operations delegated by the domain.
The [arg entityname] argument gives the name of the entity.
The [arg script] argument is a script that is evaluated in a context where
[sectref "External Entity Commands" "external entity commands"]
are available to define the characteristics of the external entity.
The [cmd eentity] command may invoked multiple times for the same
[arg entityname] and the entity definition is cumulative.

[call [cmd typealias] [arg name] [arg definition]]

The [cmd typealias] command defines a new data type name, [arg name],
and equates it to the "C" type defintion given by [arg definition].

[example {
typealias Direction_t enum {
    Up, Down
}
}]

[call [cmd interface] [arg text]]

The [cmd micca] code generator produces a header file for the domain.
The [cmd interface] command will insert [arg text] near the beginning
of the generated header file.
The [cmd interface] command may be invoked multiple times,
and the text is concatenated in the order the commands are encountered.
This command is typically used for declarations and include
files needed by other code in the domain.

[example {
interface {
    #include "myheader.h"
}
}]

[call [cmd prologue] [arg text]]

The [cmd micca] code generator produces a code file for the domain.
The [cmd prologue] command will insert [arg text] near the beginning
of the generated code file.
The [cmd prologue] command may be invoked multiple times,
and the text is concatenated in the order the commands are encountered.
This command is typically used for declarations and include
files needed by code in the domain.

[example {
prologue {
    struct Point {
        int x ;
        int y ;
    } ;
    static void scalarMultiple(int, struct Point *) ;
}
}]

[call [cmd epilogue] [arg text]]

The [cmd micca] code generator produces a code file for the domain.
The [cmd epilogue] command will insert [arg text] near the end
of the generated code file.
The [cmd epilogue] command may be invoked multiple times,
and the text is concatenated in the order the commands are encountered.
This command is typically used for local definitions
needed by code in the domain.

[example {
epilogue {
    static void
    scalarMultiple(
        int a,
        struct Point *p)
    {
        p->x *= a ;
        p->y *= a ;
        return ;
    }
}
}]

[list_end]

[section "Class Commands"]

The commands in this section may appear in the [arg script]
argument of the [cmd class] command to define the properties
of a class.

[list_begin definitions]
[call [cmd attribute] [arg name] [arg type] [arg [opt "-default value"]]\
    [arg [opt "-dependent formula"]]]

The [cmd attribute] command defines [arg name] to be an attribute of the
enclosing class.
The attribute has a data type of [arg type].
[arg Name] must be a valid "C" identifier and [arg type] must be
a valid "C" type name or a type name given by the [cmd typealias] command.
A default value for the attribute may optionally be specified using
the [option -default] option.
Default values are used in any context where the an instance of the
class is created and no value for the attribute is given in the
creation invocation.
The [arg value] of a default value must be a valid "C" initializer and
is passed directly to the generated code.
By special dispensation,
attributes of type [emph "array of character"]
([emph e.g.] char[lb]16[rb]) are treated
as NUL terminated strings in the usual "C" conventions.
If the [option -dependent] arguments is present,
the [arg name] attribute is defined as a mathematically dependent
attribute.
These types of attributes may only be read
and when they are read,
the value of the attribute is that returned by the [arg formula].
The [arg formula] value should consist of "C" code to compute the
value of the attribute.
When invoked, [arg formula] is supplied with a value of [var self]
for the class instance, a pointer to a location where the
attribute value is to be written and the number of bytes
memory for the result.
The variable name of the pointer is the same as the name of the attribute
and it is a pointer to the same type as the attribute.
The variable name of the memory size is [var size].

[example {
class Washer {
    attribute ModelName {char[20]} ; # This is a NUL terminated string
    attribute Capacity unsigned -default 4
}

class Extent {
    attribute Height int
    attribute Width int
    attribute Area int -dependent {
        *Area = self->Height * self->Width ;
    }
}

class Name {
    attribute FirstName {char[32]} ;
    attribute LastName {char[32]} ;
    attribute FullName {char[64]} -dependent {
        size_t firstLen = strlen(self->FirstName) ;
        size_t lastLen = strlen(self->LastName) ;
        *FullName = '\0' ;
        if (size >= firstLen + lastLen + 2) {
            strcat(FullName, self->FirstName)
            strcat(FullName, " ") ;
            strcat(FullName, self->LastName) ;
        }
    }
}
}]

[call [cmd classop] [arg rettype] [arg name] [arg parameters] [arg body]]

The [cmd classop] command defines a class based operation of the
enclosing class.
The name of the operation is given by the [arg name] argument and its
"C" return type is [arg rettype].
Formal parameters to the operation are given as a list
of parameter name / parameter type pairs in the [arg parameters] argument.
The code for the operation is supplied in the [arg body] argument.
Class operations do [emph not] have an implied parameter of [var self].
Class operation typically do not arise as part of a domain model
and are most useful when implementing particular search or summarizing
code on the class as a whole.

[example {
classop {struct Washer *} findById {washerID MRT_InstId} {
    // Code to find a specific washer instance.
}
}]

[call [cmd instop] [arg rettype] [arg name] [arg parameters] [arg body]]

The [cmd instop] command defines an instance based operation of the
enclosing class.
The name of the operation is given by the [arg name] argument and its
"C" return type is [arg rettype].
Formal parameters to the operation are given as a list
of parameter name / parameter type pairs in the [arg parameters] argument.
The code for the operation is supplied in the [arg body] argument.
Instance operations have an implied parameter of [var self],
which is supplied automatically to the operation upon invocation.

[example {
instop unsigned incrCapacity {howmuch unsigned} {
    return self->Capacity += howmuch ;
}
}]

[call [cmd statemodel] [arg body]]

The [cmd statemodel] command defines the states, events and transitions
of a state model which is associated with the enclosing class.
The [arg body] argument should contain invocations of
[sectref "State Model Commands" "state model commands"]
to define the properties of the class state model.

[example {
statemodel {
    # Commands to define the states, events and transitions of
    # the class state model.
}
}]

[call [cmd polymorphic] [arg event] [arg [opt "argname argtype ..."]]]

The [cmd polymorphic] command defines a polymorphic event named,
[arg event] which has optional parameters given by
[opt "argnameN argtypeN"] pairs.
A polymorphic event may only be defined on a class that serves as a
superclass in a generalization relationship.
If [arg event] requires any parameters they must be given in this command.
Polymorphic events are inherited across all generalizations in which
the enclosing class is a superclass.

[example {
polymorphic Run howfast int howlong int
polymorphic Stop
}]

[call [cmd constructor] [arg body]]

The [cmd constructor] command defines a constructor for the enclosing class.
The [arg body] argument consists of "C" code which is run
whenever a class is created during run time.
The constructor is supplied automatically with a value for the [var self]
variable.
Typically, constructors are used to initialize complex user defined
data types associated with attributes.

[call [cmd destructor] [arg body]]

The [cmd destructor] command defines a destructor for the enclosing class.
The [arg body] argument consists of "C" code which is run
whenever a class is deleted during run time.
The destructor is supplied automatically with a value for the [var self]
variable.
Typically, destructors are used to clean up complex user defined
data types associated with attributes.

[list_end]

[section "State Model Commands"]

The commands in this section are used to define the state model
for a class.

[list_begin definitions]

[call [cmd state] [arg name] [arg parameters] [arg body]]

The [cmd state] command defines a new state called, [arg name].
If the state accepts parameters, they are given in the
[arg parameters] argument as a list of parameter name / parameter type
pairs.
If the state does not accept parameters, the empty list ( {} ) should
be given.
Upon entry into the state,
the "C" code given in the [arg body] argument is executed.
The [arg body] will have a pre-defined variable named, [var self],
which is a reference to the class instance for which the state
transition happened.

[para]

The [arg body] should contain "C" code and [cmd micca] embedded
macro commands.
The embedded commands provide a convenient interface for performing
model level actions such as signaling events.
See the [cmd micca-embedded] manual page for a description of
the available embedded commands.

[example {
state Running {} {
    <%my findOneRelated car R3 R7%>
    if (car != NULL) {
        <%instance car signal Started%>
    }
}

state Activate {count int} {
    <%my update Number count%>
    <%my signal Run%>
}
}]

[call [cmd transition] [arg currstate] - [arg event] -> [arg newstate]]

The [cmd transition] command defines a state model transition
from [arg currstate] to [arg newstate] when [arg event] is received.
The [arg currstate] argument may be the name of any state in the
state model (that state need not have been defined yet) or the
pseudo-initial state "@".
If [arg currstate] is given as "@", the [arg event] is a creation event.
The [arg newstate] argument may be the name of any state or the
special non-transitioning states of "IG" (ignore),
which causes [arg event] to be ignored or "CH" (can't happen),
which results in a system error if [arg event] is received.
The "-" and "->" arguments are syntactic sugar and are ignored.

[example {
transition Running - Stop -> Stopped
transition Stopped - Run -> Running
}]

[call [cmd event] [arg name] [arg [opt "argname argtype ..."]]]

The [cmd event] command defines an event named, [arg name], with
optional parameters given by the [arg "argnameN argtypeN"] pairs.
It is not required to define events.
The names and parameter signatures will be deduced from their
usage in transition statements.
However, in some cases, particularly those where the event carrys
parameters, it may be more convenient to define the event and its
signature rather than relying on assuming a signature by virtue
of the event causing a transition into a particular state.

[example {
event Run speed int

state Running {fast int} {
}

transition Stopped - Run -> Running
}]

In this example, the [var Run] event and the [var Running] state
have the same parameter signature, despite the difference in
naming of the parameter itself.

[call [cmd initialstate] [arg state]]

The [cmd initialstate] command defines [arg state] to be the default
initial state for the state model.
Instances may be created in any state, but by default they are created
in [arg state].
If the [cmd initialstate] command does not appear in the state model
definition, then the default initial state is taken to be the lexically first
state defined in the state model.


[call [cmd defaulttrans] [arg transition]]

The [cmd defaulttrans] command defines [arg transition] to be the
default transition.
The default transition is used for all transitions not explicitly
mentioned in a [cmd transition] command.
The [arg transition] argument must be either [var IG] or [var CH].
If no [cmd defaulttrans] command is given in the state model definition,
the default transition is assumed to be [var CH].

[call [cmd final] [arg [opt "state ..."]]]

The [cmd final] command defines the [arg states] as final states.
An instance that enters a final state is automatically deleted
after its state activity is run.
The [cmd final] may appear multiple times in the state model definition.

[list_end]

[section "Assigner Commands"]
Assigner commands can appear in the [arg script] argument of the
[cmd association] command and are used to define the properties
of the association assigner.

[list_begin definitions]
[call [cmd statemodel] [arg body]]

The [cmd statemodel] command defines the state model of an assigner.
All the commands defined for state models of classes can be used
to define the state model of an assigner.
An assigner must have a state model.

[call [cmd identifyby] [arg class]]

The [cmd identifyby] command defines [arg class] as the partitioning
class for a multiple assigner.
Multiple assigners have the same number of instances as there are
instances of [arg class].

[list_end]

[section "External Entity Commands"]

External entity commands can appear in the [arg script] argument of the
[cmd eentity] command and are used to define external operations
that the entity must provide.

[list_begin definitions]

[call [cmd operation] [arg rettype] [arg name] [arg parameters] [arg body]\
        [arg [opt comment]]]

The [cmd operation] command defines an external entity operation called,
[arg name], which
the domain assumes will be supplied out side of the domain definition.
An external entity operation is translated into an external function reference.
The "C" return type of the function is given by the [arg rettype] argument.
Formal parameters to the operation are given as a list
of parameter name / parameter type pairs in the [arg parameters] argument.
"C" code may be supplied as the [arg body] argument.
By default, the code is [emph not] placed in the output,
but to aid in testing and simulation,
the [cmd micca] translator has an option to include the code in the
translated output.
If present, the optional [arg comment] argument text is inserted as
a comment in the generated header file for the domain just before the
external declaration of the external operation function.

[example {
eentity MSG {
    operation {char const *} Get_message {lang {char const *}} {} {
        Get a message from the MSG external entity.
    }
}
}]

[list_end]

[section "Population Commands"]

The population comands are used to define the intial instance population
for a domain.
Populations may be defined for classes and multiple assigners.
Single assigner populations are automatically instanciated by [cmd micca].

Base attribute values are specified as strings that are passed directly
as initializers into the output ``C'' code.
Referential values use the names of class instances to implement the
references.

Consider:
[example {
domain FlightSchedule {
    class Passenger {
        attribute Name {char[32]}
    }

    class Flight {
        attribute FlightID {char[32]}
    }

    association R1 Passenger 0..*--1 Flight
}
}]

The Passenger class is deemed to have an [var R1] referential attribute
which must be given as the name of a Flight instance.

[example {
population FightSchedule {
    class Passenger {
        instance Fred R1 f1
        instance Alice R1 f1
    }
    class Flight {
        instance f1 FlightID SWA172
    }
}
}]

The ordering of the instance definitions is arbitrary. Above the
Passenger instances may be defined before the Flight instances to which
they are related are defined.

The same pattern is used when specifying the links between a subclass
instance and its related superclass instance.

For associator classes, there are two references, one for each
participant in the association.
In that case the value of the referential attribute is a list
consisting of two pairs, the class name and an instance name for
one participant and a class name and instance name for the other
participant.

[example {
class Ownership {
    instance ow1 Acquired 2015 R23 {Owner fred Dog rover}
}
}]

For reflexive associator classes, the class names of the participants are
the same and so we resort to the keywords [option forward] and
[option backward] to specify the references in the forward and backward
directions of the association, respectively.

[example {
class Authorship {
    instance ath1 R16 {forward author2 backward author1}
}
}]

[list_begin definitions]

[call [cmd class] [arg name] [arg script]]
The [cmd class] command (in a [cmd population] command script)
defines the population for a class named, [arg class].
The [arg script] argument is a script that is evaluated in a context where
[sectref "Class Population Commands" "class population commands"]
are available to define the initial population of class instances.

[call [cmd assigner] [arg association] [arg script]]
The [cmd assigner] command defines the population the multiple
assigner for the association named, [arg association].
The [arg script] argument is a script that is evaluated in a context where
[sectref "Assigner Population Commands" "assigner population commands"]
are available to define the initial population of class instances.

[list_end]

[section "Class Population Commands"]
[list_begin definitions]
[call [cmd instance] [arg name] [arg comp1] [arg values1] [arg [opt ...]]]

The [cmd instance] command defines the attribute and relationship values 
for an instance of the enclosing class.
The instance is named, [arg name], and this name may be used to refer
to the instance when supplying relationship linkage values.
Instance names must be unque within the class to which they pertain.
The attribute and relationship values are given in pairs by the
[arg compN] / [arg valueN] arguments.
Every component of the instance must either be specified in the
[cmd instance] command or have defined default values.
An attribute may be given its default value by specifying the value
of the attribute as a dash character ( - ).
If the value of an attribute need to be a dash character, then it
must be escaped with a backslash character ( \- )

[example {
instance dog1 Name Rover Born 2010
}]

[call [cmd table] [arg heading] [arg name1] [arg values1] [arg [opt ...]]]

The [cmd table] command allows defining multiple class instance in
a more tabular layout.
The [arg heading] is a list of instance component names, i.e. attribute
names or relationship names.
Each instance is defined by a [arg name] and a list of [arg values].
The [arg values] argument is a simple list of values and they are
used as initializers in the same order as the attributes are listed
in the [arg heading].

[example {
    table       {Name       Born}\ 
    dog2        {Jumper     2012}\ 
    dog3        {Fluffy     2015}
}]

[call [cmd allocate] [arg size]]

The [cmd allocation] command requests that [arg size] number of
[emph additional] storage slots be allocated for the class.
The maximum number of instances of the class is the sum of the
number of instances in the initial instance population plus [arg size].

[list_end]

[section "Assigner Population Commands"]
[list_begin definitions]
[call [cmd instance] [arg name] [arg idinstance]]

The [cmd instance] command (in the context of an assigner population script)
defines a multiple assigner instance called, [arg name], and set the
identifying instance of the assigner to [arg idinstance].
The [arg idinstance] must be the name of a class instance from the
identifying class specified when the multiple assigner was defined
(see [cmd identifyby] command above).

[list_end]

[see_also micca]

[keywords executable model]

[manpage_end]
