#!/usr/bin/env tclsh
# This software is copyrighted 2013 by G. Andrew Mangogna.
# The following terms apply to all files associated with the software unless
# explicitly disclaimed in individual files.
# 
# The authors hereby grant permission to use, copy, modify, distribute,
# and license this software and its documentation for any purpose, provided
# that existing copyright notices are retained in all copies and that this
# notice is included verbatim in any distributions. No written agreement,
# license, or royalty fee is required for any of the authorized uses.
# Modifications to this software may be copyrighted by their authors and
# need not follow the licensing terms described here, provided that the
# new terms are clearly indicated on the first page of each file where
# they apply.
# 
# IN NO EVENT SHALL THE AUTHORS OR DISTRIBUTORS BE LIABLE TO ANY PARTY FOR
# DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES ARISING
# OUT OF THE USE OF THIS SOFTWARE, ITS DOCUMENTATION, OR ANY DERIVATIVES
# THEREOF, EVEN IF THE AUTHORS HAVE BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE AUTHORS AND DISTRIBUTORS SPECIFICALLY DISCLAIM ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE, AND NON-INFRINGEMENT.  THIS SOFTWARE
# IS PROVIDED ON AN "AS IS" BASIS, AND THE AUTHORS AND DISTRIBUTORS HAVE
# NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES, ENHANCEMENTS,
# OR MODIFICATIONS.
# 
# GOVERNMENT USE: If you are acquiring this software on behalf of the
# U.S. government, the Government shall have only "Restricted Rights"
# in the software and related documentation as defined in the Federal
# Acquisition Regulations (FARs) in Clause 52.227.19 (c) (2).  If you
# are acquiring the software on behalf of the Department of Defense,
# the software shall be classified as "Commercial Computer Software"
# and the Government shall have only "Restricted Rights" as defined in
# Clause 252.227-7013 (c) (1) of DFARs.  Notwithstanding the foregoing,
# the authors grant the U.S. Government and others acting in its behalf
# permission to use and distribute the software in accordance with the
# terms specified in this license.
#
#*++
# PROJECT:
#   mrtools
#
# MODULE:
#   oomoore.test -- unit test cases for oomoore package
#
# ABSTRACT:
#
#*--
#

package require Tcl 8.6
package require cmdline

source ../src/oomoore.tcl

puts "testing version: [package require oomoore]"

set optlist {
    {level.arg warn {Log debug level}}
}
array set options [::cmdline::getKnownOptions argv $optlist]

package require tcltest
eval tcltest::configure $argv

namespace eval ::oomoore::test {
    namespace import ::tcltest::*

    test model-1.0 {
        build simple model
    } -setup {
    } -cleanup {
    } -body {
        oomoore model create simple {
            state s1 {} {
                puts s1
            }
            transition s1 - e1 -> s2
            state s2 {} {
                puts s2
            }
        }
    } -result {::oomoore::test::simple}

    test states-1.0 {
    } -setup {
    } -cleanup {
    } -body {
        simple states
    } -result {s1 s2}

    test events-1.0 {
    } -setup {
    } -cleanup {
    } -body {
        simple events
    } -result {e1}

    test transitions-1.0 {
    } -setup {
    } -cleanup {
    } -body {
        simple transitions
    } -result {{s1 e1 s2} {s2 e1 CH}}

    test initialstate-1.0 {
        check the initial state of simple
    } -setup {
    } -cleanup {
    } -body {
        simple initialstate
    } -result {s1}

    test defaulttransition-1.0 {
        check the defaulttransition simple
    } -setup {
    } -cleanup {
    } -body {
        simple defaulttransition
    } -result {CH}

    test model-2.0 {
        state model error -- transition has non-existant state
    } -setup {
    } -cleanup {
    } -body {
        oomoore model create err1 {
            state s1 {} {
                puts s1
            }
            transition s1 - e1 -> s2
        }
    } -result {unknown destination state in transition, "s1 - e1 -> s2"}\
        -returnCodes error

    test machine-1.0 {
        generate a state machine
    } -setup {
    } -cleanup {
        s1 destroy
    } -body {
        simple create s1
    } -result {::oomoore::test::s1}

    test transition-1.0 {
        transition a state machine
    } -setup {
        simple create s2
    } -cleanup {
        s2 destroy
    } -body {
        s2 receive e1
    } -output {s2
} -result {}

    test transition-2.0 {
        events with parameters
    } -setup {
        oomoore model create reflex {
            state s2 {a b} {
                puts "[self] $a $b"
            }
            transition s1 - e1 -> s2
            transition s2 - e2 -> s1
            transition s2 - e1 -> s2
            state s1 {a} {
                puts "[self] $a"
            }
            initialState s1
        }
        reflex create r1
    } -cleanup {
        r1 destroy
    } -body {
        r1 receive e1 foo bar
    } -output {::oomoore::test::r1 foo bar
} -result {}

    test initialstate-2.0 {
        check the initial state of reflex
    } -setup {
    } -cleanup {
    } -body {
        reflex initialstate
    } -result {s1}

    test transition-3.0 {
        reflexive transition
    } -setup {
        reflex create r1
    } -cleanup {
        r1 destroy
    } -body {
        r1 receive e1 foo bar
        r1 receive e1 baz junk
    } -output {::oomoore::test::r1 foo bar
::oomoore::test::r1 baz junk
} -result {}

    test ignored-1.0 {
        ignored transition
    } -setup {
        oomoore model create m2 {
            state s1 {} {
                puts "[self] s1"
            }
            transition s1 - e2 -> s2
            transition s1 - e3 -> s3
            state s2 {} {
                puts "[self] s2"
            }
            transition s2 - e2 -> IG
            transition s2 - e3 -> CH
            state s3 {} {
                puts "[self] s3"
            }
            transition s3 - e2 -> s2
            transition s3 - e1 -> s1
        }
        m2 create i2
    } -cleanup {
        i2 destroy
    } -body {
        i2 receive e2
        i2 receive e2
    } -output {::oomoore::test::i2 s2
} -result {}

    test event-1.0 {
        bad event name
    } -setup {
        m2 create i2
    } -cleanup {
        i2 destroy
    } -body {
        i2 receive e4
    } -result {unknown event, "e4"} -returnCodes error

    test canthappen-1.0 {
        can't happen transition
    } -setup {
        m2 create i2
    } -cleanup {
        i2 destroy
    } -body {
        i2 receive e2
        i2 receive e3
    } -result {can't happen transition: s2 - e3 -> CH} -returnCodes error

    test isolated-1.0 {
        detect state models with isolated states
    } -setup {
    } -cleanup {
    } -body {
        oomoore model create m3 {
            state s1 {} {
                puts "$self s1"
            }
            transition s1 - e2 -> s2
            state s2 {} {
                puts "$self s2"
            }
            transition s2 - e2 -> IG
            transition s2 - e3 -> CH
            state s3 {} {
                puts "$self s3"
            }
        }
    } -result {state model has isolated state(s): "s3"} -returnCodes error

    test generate-1.0 {
        generate events for async dispatch
    } -setup {
        m2 create i3
    } -cleanup {
        i3 destroy
    } -body {
        i3 loglevel info
        i3 signal {} e3
        after 0 set ::done 1
        vwait ::done
        i3 currentstate
    } -result {s3}

    test generate-2.0 {
        generate multiple events for async dispatch
    } -setup {
        m2 create i3
    } -cleanup {
        i3 destroy
    } -body {
        i3 signal {} e3
        i3 signal {} e2
        after 100 set ::done 1
        vwait ::done
        i3 currentstate
    } -result {s2}

    test generate-3.0 {
        async dispatch of self-directed events
    } -setup {
        oomoore model create m4 {
            state s1 {a} {
                puts "s1: $a"
                set ::done 1
            }
            transition s1 - e1 -> s2
            state s2 {b} {
                puts "s2: $b"
                my signal [self] e2 $b
            }
            transition s2 - e2 -> s1
        }
        m4 create i4
    } -cleanup {
        i4 destroy
    } -body {
        i4 signal {} e1 foo
        vwait ::done
        i4 currentstate
    } -result {s1} -output {s2: foo
s1: foo
}

    test generate-4.0 {
        self-directed events take precedence
    } -setup {
        m4 create i4
    } -cleanup {
        i4 destroy
    } -body {
        i4 signal {} e1 foo
        i4 signal {} e1 bar
        after 100 set ::sync 1
        vwait ::sync
        i4 currentstate
    } -result {s1} -output {s2: foo
s1: foo
s2: bar
s1: bar
}

    test delayed-1.0 {
        post a delayed event
    } -setup {
        m2 create i2
    } -cleanup {
        i2 destroy
    } -body {
        i2 delayedSignal 100 {} e2
        after 200 set ::sync 1
        vwait ::sync
        i2 currentstate
    } -result {s2} -output {::oomoore::test::i2 s2
}

    test delayed-2.0 {
        delayed events delivered in order
    } -setup {
        m2 create i2
    } -cleanup {
        i2 destroy
    } -body {
        i2 delayedSignal 200 {} e2
        i2 delayedSignal 100 {} e3
        after 300 set ::sync 1
        vwait ::sync
        i2 currentstate
    } -result {s2} -output {::oomoore::test::i2 s3
::oomoore::test::i2 s2
}

    test delayed-3.0 {
        duplicate delayed events -- delivered in order
    } -setup {
        m4 create i4
    } -cleanup {
        i4 destroy
    } -body {
        i4 delayedSignal 100 {} e1 foo
        i4 delayedSignal 100 {} e1 bar
        after 300 set ::sync 1
        vwait ::sync
        i4 currentstate
    } -result {s1} -output {s2: foo
s1: foo
s2: bar
s1: bar
}

    test cancel-1.0 {
        cancel a delayed event before it expires
    } -setup {
        m2 create i2
    } -cleanup {
        i2 destroy
    } -body {
        set sigtoken [i2 delayedSignal 300 {} e2]
        after 100 after cancel $sigtoken

        after 300 set ::sync 1
        vwait ::sync
        i2 currentstate
    } -result {s1}

    test cancel-2.0 {
        cancel a delayed event after it expires
    } -setup {
        m2 create i2
    } -cleanup {
        i2 destroy
    } -body {
        set sigtoken [i2 delayedSignal 100 {} e2]
        after 200 after cancel $sigtoken

        after 300 set ::sync 1
        vwait ::sync
        i2 currentstate
    } -result {s2} -output {::oomoore::test::i2 s2
}

    test cancel-3.0 {
        cancel first delayed event
    } -setup {
        m2 create i2
    } -cleanup {
        i2 destroy
    } -body {
        set sigtoken [i2 delayedSignal 200 {} e2]
        i2 delayedSignal 300 {} e2
        after 100 after cancel $sigtoken

        after 400 set ::sync 1
        vwait ::sync
        i2 currentstate
    } -result {s2} -output {::oomoore::test::i2 s2
}

    cleanupTests
}

namespace delete ::oomoore::test

if 0 {
    # This is just a template for additional tests.

    test X-1.0 {
    } -setup {
    } -cleanup {
    } -body {
    } -result {}
}
